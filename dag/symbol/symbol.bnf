/* Lexical part */

_digit : '0'-'9' ;

_letter : 'a'-'z' | 'A'-'Z' ;

_idchar : _letter | _digit | '_' ;

int : '1'-'9' { _digit } ;

_time_unit : 'Y' | 'M' | 'D' | 'h' | 'm' | 's' ;

date : { _time_unit | ' ' | ':' | '-' } ;

id : { _letter | '_' } { _idchar } ;

!whitespace : ' ' | '\t' | '\n' | '\r' ;

/* Syntax part */

<<
import(
  "github.com/crackcell/hpipe/dag/symbol/token"
  "github.com/crackcell/hpipe/dag/symbol/ast"
)
>>

Expr
  : Expr "+" Term << ast.NewOperatorFromParser($0.(*ast.Expr), "+", $2.(*ast.Expr)) >>
  | Expr "-" Term << ast.NewOperatorFromParser($0.(*ast.Expr), "-", $2.(*ast.Expr)) >>
  | Term
  ;

Term
  : Term "*" Factor << ast.NewOperatorFromParser($0.(*ast.Expr), "*", $2.(*ast.Expr)) >>
  | Term "/" Factor << ast.NewOperatorFromParser($0.(*ast.Expr), "/", $2.(*ast.Expr)) >>
  | Factor
  ;

Factor
  : "(" Expr ")" << $1, nil >>
  | int          << ast.NewIntFromParser(string($0.(*token.Token).Lit)) >>
  | Variable
  ;

Variable
	: "${" Builtins "}" << $1, nil >>
	| "$" id            << ast.NewVarFromParser(string($1.(*token.Token).Lit)) >>
  ;

Builtins
	: date     << ast.NewDateFromParser(string($0.(*token.Token).Lit)) >>
	;