/* Lexical part */

_digit : '0'-'9' ;

_letter : 'a'-'z' | 'A'-'Z' ;

_idchar : _letter | _digit | '_' | '.' ;

int : '1'-'9' { _digit } ;

time_format : { 'Y' | 'M' | 'D' | 'h' | 'm' | 's' | ' ' | ':' | '-' };

id : { _letter | '_' } { _idchar } ;

string : '"' { _idchar } '"' ;

!whitespace : ' ' | '\t' | '\n' | '\r' ;

/* Syntax part */

<<
import(
  "github.com/crackcell/hpipe/dag/symbol/token"
  "github.com/crackcell/hpipe/dag/symbol/ast"
)
>>

StmtList
  : Stmt              << ast.NewStmtList($0.(*ast.Stmt)), nil >>
  | StmtList ";" Stmt << ast.AppendStmtList($0.([]*ast.Stmt), $2.(*ast.Stmt)), nil >>
  ;

Stmt
  : "$" id "=" Expr << ast.NewOperatorFromParser(ast.NewLeftID(string($1.(*token.Token).Lit)), "=", $3.(*ast.Stmt)) >>
  ;

Expr
  : Expr "+" Term << ast.NewOperatorFromParser($0.(*ast.Stmt), "+", $2.(*ast.Stmt)) >>
  | Expr "-" Term << ast.NewOperatorFromParser($0.(*ast.Stmt), "-", $2.(*ast.Stmt)) >>
  | Term
  ;

Term
  : Term "*" Factor << ast.NewOperatorFromParser($0.(*ast.Stmt), "*", $2.(*ast.Stmt)) >>
  | Term "/" Factor << ast.NewOperatorFromParser($0.(*ast.Stmt), "/", $2.(*ast.Stmt)) >>
  | Factor
  ;

Factor
  : "(" Expr ")" << $1, nil >>
  | int          << ast.NewIntFromParser(string($0.(*token.Token).Lit)) >>
	| Date         << $0, nil >>
  | Env          << $0, nil >>
 	| Id					 << $0, nil >>
	| string       << ast.NewStringFromParser(string($0.(*token.Token).Lit)) >>
  ;

Id
	: "$" id << ast.NewRightIDFromParser(string($1.(*token.Token).Lit)) >>
	;

Date
	: "${date:" time_format "}" << ast.NewDateFromParser(string($1.(*token.Token).Lit)) >>
	;

Env
	: "${env:" id "}" << ast.NewEnvFromParser(string($1.(*token.Token).Lit)) >>
	;